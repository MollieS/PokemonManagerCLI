plugins {
    id "com.github.kt3k.coveralls" version "2.6.3"
    id "jacoco"
}

group 'PokemonManagerCLI'
version '2.0.0'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'lib'
    }
}

configurations{
    driver
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile name: 'PokemonManager-1.4.0'
    compile 'com.google.code.gson:gson:2.7'
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1208.jre7'
    driver group: 'org.postgresql', name: 'postgresql', version: '9.4.1208.jre7'
}

task(run, dependsOn: 'classes', type: JavaExec) {
    main = "pokemoncli.Main"
    classpath = sourceSets.main.runtimeClasspath
}

run {
    standardInput = System.in
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/Main.*',
                            '**/ConsoleDisplay.*',
                            '**/ConsoleInput.*'
                    ])
        })
    }
}

task sql << {
    URLClassLoader loader = GroovyObject.class.classLoader
    configurations.driver.each {File file ->
        loader.addURL(file.toURL())
    }
    def driverName = 'org.postgresql.Driver'
    groovy.sql.Sql sql = groovy.sql.Sql.newInstance('jdbc:postgresql://localhost:5432/pokemon', 'test', 'test', driverName)
    sql.execute 'create table pokemon (name varchar(50) unique, height varchar(10))'
    sql.execute 'create table abilities (name varchar(50), ability varchar(50))'
}
